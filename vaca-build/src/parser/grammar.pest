program = { vacaimport* ~ form* }

lib = {
  export_list ~
  assingment_list*
}

form = {
  assingment_list
| export_list
| function
| macrodef
| array_list
| code_block
| call
| literal
| dontcare
}

literal = {
  float
| integer
| char
| string
| bool
| nil
| atom
| symbol
}

keyword   = { bool | nil }
operators = { ":" | "<)" | "(>" | "<(" | "[(" | "#(" | "@(" | "{" | "[" | "]" | "}" | ")" | "_" | "->" }

symbol = @{
  !keyword ~ ('a'..'z' | 'A'..'Z' ) ~ ('a'..'z' | 'A'..'Z' | '0'..'9' | "-" | "/")* ~ "!"? ~ "?"? ~ "'"
| !operators ~ ("!" | "@" | "#" | "$" | "%" | "&" | "*" | "-" | "+" | "=" | "`" | "~" | "^" | ":" | ">" | "<" | "," | "|" | "\\" | "/" | "?")+
}

dontcare = { "_" }
atom           = @{ ":" ~ ('a'..'z' | 'A'..'Z' ) ~ ('a'..'z' | 'A'..'Z' | '0'..'9' | "-")* }
float          = @{ "-"? ~ ('0'..'9')* ~ "." ~ ('0'..'9')+ | ('0'..'9')+ ~ "." ~ ('0'..'9')* }
integer        = @{ "-"? ~ ('0'..'9')+ }

string         = ${ "\"" ~ string_content ~ "\"" }
//string_content = @{ (!"\"" ~ ANY)* }
string_content = @{ char_content* }

char           = ${ "'" ~ char_content ~ "'" }
//char_content   = @{ (!"'" ~ ANY)? }
char_content = {
    !("\"" | "\\") ~ ANY
    | "\\" ~ ("\"" | "\\" | "/" | "b" | "f" | "n" | "r" | "t")
    | "\\" ~ ("u" ~ ASCII_HEX_DIGIT{4})
}

bool           =  { "true" | "false" }
nil            =  { "nil" }

assingment_list = { "#(" ~ assingment* ~ ")" }
export_list     = { "@(" ~ assingment* ~ ")" }
assingment      = { symbol ~ form }

array_list = { "[" ~ form* ~ "]" }

function   = { "<(" ~ (symbol ~ (!"->" ~ symbol)* ~ "->")? ~ form ~ ")" }
macrodef   = { "[(" ~ (symbol ~ (!"->" ~ symbol)* ~ "->")? ~ form ~ ")" }
vacaimport   = { "<)" ~ symbol ~ symbol ~ ("->" ~ symbol+)?~ "(>" }
call       = { "(" ~ form+ ~ ")" }
code_block = { "{" ~ form* ~ "}" }

WHITESPACE = _{ " " | "\t" | "\n" | "\r" }
COMMENT    = _{ ";;" ~ (!";;" ~ ANY)* ~ ";;" | ";" ~ (!NEWLINE ~ ANY)* ~ NEWLINE }
